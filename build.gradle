group 'cn.edu.fudan'

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'idea'

repositories {
    maven {
        url 'http://maven.oschina.net/content/groups/public/'
    }
}

ext {
    tomcatVersion = '7.0.64'
    springVersion = '4.2.2.RELEASE'
    commonsFileuploadVersion = '1.3.1'
    jacksonVersion = '2.6.3'
    mysqlVersion = '5.1.37'
    springfoxVersion = '2.2.2'
    logbackVersion = '1.1.3'
    junitVersion = '4.12'
    httpComponentsVersion = '4.5.1'
}

dependencies {

    testCompile "junit:junit:${junitVersion}"

    compile "org.apache.tomcat:tomcat-servlet-api:${tomcatVersion}"

    compile "org.springframework:spring-webmvc:${springVersion}"
    compile "org.springframework:spring-jdbc:${springVersion}"
    testCompile "org.springframework:spring-test:${springVersion}"
    compile "commons-fileupload:commons-fileupload:${commonsFileuploadVersion}"

    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

    compile "mysql:mysql-connector-java:${mysqlVersion}"

    compile "io.springfox:springfox-swagger2:${springfoxVersion}"
    compile "io.springfox:springfox-swagger-ui:${springfoxVersion}"

    compile "ch.qos.logback:logback-classic:${logbackVersion}"

    compile "org.apache.httpcomponents:httpclient:${httpComponentsVersion}"
    compile "org.apache.httpcomponents:httpmime:${httpComponentsVersion}"
}

task generateSDK(type: Jar, dependsOn: compileJava) {
    baseName 'Sport-App-Backend-SDK'
    from('build/classes/main') {
        include 'cn/edu/fudan/sport/domain/**'
        include 'cn/edu/fudan/sport/vo/**'
        include 'cn/edu/fudan/sport/server/**'
    }
    from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        include 'com/fasterxml/jackson/**'
        include 'org/apache/http/**'
        include 'org/apache/commons/**'
    }
}
